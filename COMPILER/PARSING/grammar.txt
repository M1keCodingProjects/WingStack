//
?   : 0 or 1 times (optional)
*   : 0 or more times
+   : 1 or more times
<?> : 0 or more items in group in any order
//

Program    : Expression*

Expression : (Procedure | Assignment) ";"?

Procedure  : PrintProc | WhenProc | LoopProc | NextProc | ExitProc | MakeProc

PrintProc  : "print" StackExpr

StackExpr  : (StackValue | STACKOP | CallChain)+

StackValue : NUM | STR

CallChain  : Property (("." Property) | IDProp)*

Property   : IDProp | WORD

IDProp     : "[" StackExpr "]"

WhenProc   : "when" StackExpr "loop"? Block ElseProc?

Block      : ("{" Program "}") | ("then" Expression)

ElseProc   : "else" (WhenProc | Block)

LoopProc   : "loop" StackExpr Block

NextProc   : "next"

ExitProc   : "exit" StackExpr?

MakeProc   : "make" ("frozen" | "dynamic" | "global")<?> Assignment

Assignment : CallChain (":" Type)? "=" StackExpr

Type       : SingleType ("|" SingleType)*

SingleType : (TYPE | WORD) | "[" Type "]"