//
?   : 0 or 1 times (optional)
*   : 0 or more times
+   : 1 or more times
<?> : 0 or more items in group in any order
//

Program    : Expression*

Expression : (Procedure | Assignment) ";"?

Procedure  : PrintProc | IfProc | LoopProc | NextProc | ExitProc | MakeProc | FreeProc

PrintProc  : "print" StackExpr

StackExpr  : (StackValue | STACKOP | CallChain)+

StackValue : NUM | BIN | STR

CallChain  : Property (("." Property) | IDProp)*

Property   : IDProp | WORD

IDProp     : "[" StackExpr "]"

IfProc     : "if" StackExpr "loop"? Block ElseProc?

Block      : ("{" Program "}") | ("then" Expression)

ElseProc   : "else" (IfProc | Block)

LoopProc   : "loop" StackExpr Block

NextProc   : "next"

ExitProc   : "exit" StackExpr?

MakeProc   : "make" ("const" | "dynamic" | "global" | "type")<?> Assignment

Assignment : CallChain (":" Type)? ((ASSIGN_OP StackExpr) | INCR_OP)

Type       : SingleType ("|" SingleType)*

SingleType : (TYPE | WORD) | "[" Type "]"

FreeProc   : "free" CallChain