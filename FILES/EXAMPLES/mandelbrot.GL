make t :num = time;
make const W = 60;
make const H = 45;
make const MAX_ITERS = 100;
make const REAL_SET = -2 1;
make const IMG_SET  = -1 1;

make const COLORS :[str|void] = [];
make i := 0;
loop 16 {
  make COLORS[i] =
    "$<background-color:rgb("
    rand 155 * 100 + int ","
    rand 155 * 100 + int ","
    rand 155 * 100 + int
    ");>" str;
  
  i++;
}

make image :str = "$<display:block; line-height:.5rem>";

make y := 0;
loop H {
  make x := 0;
  loop W { 
    make complex :[num] =
      REAL_SET[0] x W / REAL_SET[1] REAL_SET[0] - * +
      IMG_SET[0]  y H / IMG_SET[1]  IMG_SET[0]  - * +;
    
    ####
    make z :[num] = 0 0;
    make n := 0;
    make d :num = 0;
    if d 2 <= n MAX_ITERS < and loop {
      make p :[num] = z[0] 2 ^ z[1] 2 ^ - 2 z[0] z[1] * *;
      
      z = p[0] complex[0] + p[1] complex[1] +;
      
      d = z[0] 2 ^ z[1] 2 ^ + 0.5 ^;
      n++;
    }
    ####
    if d 2 <= then image += " ";
    else image += COLORS[n 16 %] " <$>" str;
    x++;
  }
  image += "&EOL";
  y++;
}

print image "<$>" +;

print time t - int " ms." str;