//
? : 0 or 1 times (optional)
* : 0 or more times
+ : 1 or more times
//

Program    : Expression*

Expression : (Procedure | Assignment) ";"?

Procedure  : PrintProc | WhenProc | LoopProc | NextProc | ExitProc

PrintProc  : "print" StackExpr

StackExpr  : (StackValue | STACKOP | CallChain)+

StackValue : NUM | STR

CallChain  : Property (("." Property) | IDProp)*

Property   : IDProp | WORD

IDProp     : "[" StackExpr "]"

WhenProc   : "when" StackExpr "loop"? Block ElseProc?

Block      : ("{" Program "}") | ("then" Expression)

ElseProc   : "else" (WhenProc | Block)

LoopProc   : "loop" StackExpr Block

NextProc   : "next"

ExitProc   : "exit" StackExpr?

Assignment : CallChain (":" Type)? "=" StackExpr

Type       : SingleType ("|" SingleType)*

SingleType : (TYPE | WORD) | "[" Type "]"