# Initialize data
make const EOL             := 10 char;
make const boardData       := 0 0 0 0 0 0 0 0 0;
make const availableMoves  := b111111111;
make const symbolMap :[str] = "$<color:var(--error-col)>X<$>" " " "$<color:var(--str-col)>O<$>";
make const players   :[str] = "Bot" "Player";

# Game loop
make turn := b0;
loop 9 {
  print "It's now " players[turn int] "'s turn!" str;
  make move :int = -1;
  if turn { # Player moves
    print "Make a valid move [0-8]";
    if TRUE loop {  
      move = inp;
      if move 0 < move 8 > or {
        print "Your move must be between 0 and 8, boundaries included.";
        next;
      }
      if availableMoves[move] not {
        print "Spot " move " on the board is taken!" str;
        next;
      }
      exit;
    }
  }
  else { # Bot moves
    print "Bot is deciding..";
    move = rand 9 * int;
    if availableMoves[move] not loop {
      move = move 1 +;
      if move 9 == then move = 0;
    }
  }
  
  availableMoves[move] = FALSE;
  boardData[move] = 2 turn ? 1 -;

  # Print the board
  make board := "$<display:block; line-height:1rem>";
  make i := 0;
  loop 3 {
    if i then board = board "$<color:var(--idle-text-col)>─╋─╋─<$>" EOL str;
    loop 3 {
      board =
        board symbolMap[boardData[i] 1 +] "$<color:var(--idle-text-col)>┃<$>" EOL
        i 1 + 3 % not dup not rot> ? swap rot< ? str;
      
      i = i 1 +;
    }
  }
  print "\clear";
  print board 10 char + "<$>" str;
  
  # Set the score
  i = 0;
  make win = FALSE;
  loop 3 { # check rows
    if boardData[i] boardData[i 1 +] boardData[i 2 +] + + 2 ^ 9 == {
      win = TRUE;
      exit;
    }
    i = i 3 +;
  }
  
  if win not {
    i = 0;
    loop 3 { # check cols
      if boardData[i] boardData[i 3 +] boardData[i 6 +] + + 2 ^ 9 == {
        win = TRUE;
        exit;
      }
      i = i 1 +;
    }
  }

  # check diagonals
  if win not boardData[0] boardData[4] boardData[8] + + 2 ^ 9 ==
             boardData[2] boardData[4] boardData[6] + + 2 ^ 9 == or and then win = TRUE;
  
  if win {
    print players[turn] " won!" +;
    exit;
  };
  turn = turn not;
}

print "Game is over! Run script to play again.";